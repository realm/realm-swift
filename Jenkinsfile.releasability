xcodeVersions = ['12.2', '12.4', '12.5.1', '13.0']
platforms = ['osx', 'ios', 'watchos', 'tvos', 'catalyst']
carthagePlatforms = ['osx', 'ios', 'watchos', 'tvos']
platformNames = ['osx': 'macOS', 'ios': 'iOS', 'watchos': 'watchOS', 'tvos': 'tvOS', 'catalyst': 'Catalyst']
carthageXcodeVersion = '13.0'
objcXcodeVersion = '12.2'
docsSwiftVersion = '5.5'

def installationTest(platform, test, language) {
  return {
    node('osx') {
      deleteDir()
      unstash 'source'

      if (test == "dynamic" || test == "static") {
        unstash "${language}-packaged"
      }

      sh """
      hostname
      export REALM_XCODE_VERSION=${carthageXcodeVersion}
      archive=\$(echo \$PWD/realm-${language}-*.zip)

      cd examples/installation
      if [[ -f \$archive ]]; then
        mv \$archive .
        unzip realm-${language}-*.zip
        rm realm-${language}-*.zip
        mv realm-${language}-* realm-${language}-latest
      fi

      ./build.sh test-${platform}-${language}-${test}
      """
    }
  }
}

def spmInstallationTest(platform) {
  return {
    node('osx') {
      deleteDir()
      unstash 'source'

      sh """
      hostname
      export REALM_XCODE_VERSION=${carthageXcodeVersion}
      cd examples/installation
      ./build.sh test-${platform}-spm
      """
    }
  }
}

def doBuild() {
  stage('prepare') {
    node('docker') {
      deleteDir()

      checkout(
        [
          $class           : 'GitSCM',
          branches         : scm.branches,
          gitTool          : 'native git',
          extensions       : scm.extensions + [[$class: 'CleanCheckout']],
          userRemoteConfigs: scm.userRemoteConfigs,
        ]
      )

      stash includes: '**', name: 'source'
    }
  }

  stage('build') {
    def parallelBuilds = [
      'Docs': {
        node('osx') {
          deleteDir()
          unstash 'source'
          sh """
          hostname
          export REALM_SWIFT_VERSION=${docsSwiftVersion}
          export PATH='/Users/realm/.rbenv/bin:/Users/realm/.rbenv/shims:/usr/local/bin:/usr/bin:/bin:/usr/sbin:/sbin:/Users/realm/.gems/bin'
          ./scripts/reset-simulators.sh
          ./build.sh docs
          cd docs
          zip -r objc-docs.zip objc_output
          zip -r swift-docs.zip swift_output
          """
          dir('docs') {
            archiveArtifacts artifacts: '*-docs.zip'
          }
        }
      },

      'Examples': {
        node('osx') {
          deleteDir()
          unstash 'source'
          sh './build.sh package-examples'
          stash includes: 'realm-examples.zip', name: 'examples'
        }
      },

      'iOS Obj-C static': {
        node('osx') {
          deleteDir()
          unstash 'source'
          sh "REALM_XCODE_VERSION=${objcXcodeVersion} ./build.sh package-ios-static"
          dir("build/ios-static") {
            stash includes: "realm-framework-ios-static.zip", name: "ios-static"
          }
        }
      }
    ]

    for (def p in carthagePlatforms) {
      def platform = p
      def platformName = platformNames[platform]
      parallelBuilds["${platformName} Carthage"] = {
        node('osx') {
          deleteDir()
          unstash 'source'
          sh """
          hostname
          export REALM_XCODE_VERSION=${carthageXcodeVersion}
          . ./scripts/swift-version.sh
          set_xcode_and_swift_versions

          # Carthage scans every xcodeproj in the directory looking for
          # targets. This can be very slow and even spuriously time out, so
          # remove the ones we don't want it to build.
          rm -r examples plugin

          # For whatever reason 'xcodebuild -list' is very slow sometimes which
          # makes Carthage time out, but it's a lot faster if no simulators
          # exist, so delete them all first and only create a single simulator
          # for each platform.
          ./scripts/reset-simulators.rb -firstOnly

          # Building the iOS static scheme just wastes time, so delete it
          rm 'Realm.xcodeproj/xcshareddata/xcschemes/Realm iOS static.xcscheme'

          carthage build --no-skip-current --platform ${platform} --derived-data DerivedData
          carthage archive --output Carthage-${platform}.framework.zip
          """
          stash includes: "Carthage-${platform}.framework.zip",
                name: "${platform}-carthage"
        }
      }
    }

    for (def p in platforms) {
      def platform = p
      def platformName = platformNames[platform]
      for (def v in xcodeVersions) {
        def xcodeVersion = v
        parallelBuilds["${platformName} ${xcodeVersion}"] = {
          node('osx') {
            deleteDir()
            unstash 'source'
            sh "REALM_XCODE_VERSION=${xcodeVersion} ./build.sh package ${platform}"
            dir("build/${platform}") {
              stash includes: "realm-framework-${platform}-${xcodeVersion}.zip",
                    name: "${platform}-${xcodeVersion}"
            }
          }
        }
      }
    }

    parallel parallelBuilds
  }

  stage('package') {
    parallel (
      "Obj-C": {
        node('osx') {
          deleteDir()

          for (def platform in platforms) {
            unstash "${platform}-${objcXcodeVersion}"
          }

          unstash 'ios-static'
          unstash 'examples'
          unstash 'source'

          sh "REALM_XCODE_VERSION=${objcXcodeVersion} ./build.sh package-release objc"
          stash includes: 'realm-objc-*.zip', name: 'objc-packaged'
          archiveArtifacts artifacts: 'realm-objc-*.zip'
        }
      },
      "Swift": {
        node('osx') {
          deleteDir()

          for (def platform in platforms) {
            for (def xcodeVersion in xcodeVersions) {
              unstash "${platform}-${xcodeVersion}"
            }
          }

          unstash 'examples'
          unstash 'source'

          sh './build.sh package-release swift'
          stash includes: 'realm-swift-*.zip', name: 'swift-packaged'
          archiveArtifacts artifacts: 'realm-swift-*.zip'
        }
      },
      "Carthage": {
        node('osx') {
          deleteDir()

          for (def platform in carthagePlatforms) {
            unstash "${platform}-carthage"
          }

          sh '''
          for zip in Carthage-*.framework.zip; do
            ditto -xk $zip merged/
          done

          ditto -ck merged/ Carthage.framework.zip
          '''

          archiveArtifacts artifacts: 'Carthage.framework.zip'
        }
      }
    )
  }

  stage('test') {
    def parallelBuilds = [
      'Test Obj-C Examples': {
        node('osx') {
          deleteDir()

          unstash 'objc-packaged'

          def sha = params.sha
          sh """
          hostname
          curl -O https://raw.githubusercontent.com/realm/realm-cocoa/${sha}/build.sh
          mkdir -p scripts
          curl https://raw.githubusercontent.com/realm/realm-cocoa/${sha}/scripts/swift-version.sh -o scripts/swift-version.sh
          curl https://raw.githubusercontent.com/realm/realm-cocoa/${sha}/scripts/reset-simulators.sh -o scripts/reset-simulators.sh
          curl https://raw.githubusercontent.com/realm/realm-cocoa/${sha}/scripts/reset-simulators.rb -o scripts/reset-simulators.rb
          chmod +x scripts/reset-simulators.rb

          sh build.sh package-test-examples-objc
          """
        }
      },

      'Test Swift Examples': {
        node('osx') {
          deleteDir()

          unstash 'swift-packaged'

          def sha = params.sha
          sh """
          hostname
          curl -O https://raw.githubusercontent.com/realm/realm-cocoa/${sha}/build.sh
          mkdir -p scripts
          curl https://raw.githubusercontent.com/realm/realm-cocoa/${sha}/scripts/swift-version.sh -o scripts/swift-version.sh
          curl https://raw.githubusercontent.com/realm/realm-cocoa/${sha}/scripts/reset-simulators.sh -o scripts/reset-simulators.sh
          curl https://raw.githubusercontent.com/realm/realm-cocoa/${sha}/scripts/reset-simulators.rb -o scripts/reset-simulators.rb
          chmod +x scripts/reset-simulators.rb

          sh build.sh package-test-examples-swift
          """
        }
      },

      'Test iOS static': {
        node('osx') {
          deleteDir()
          unstash 'source'

          sh './scripts/reset-simulators.rb'
          sh 'sh build.sh test-ios-static'
        }
      },

      'Test macOS': {
        node('osx') {
          deleteDir()
          unstash 'source'
          sh '''
            export REALM_DISABLE_METADATA_ENCRYPTION=1
            sh build.sh test-osx
          '''
        }
      }
    ]

    for (def platform in ["osx", "ios", "watchos"]) {
      def platformName = platformNames[platform]
      for (def test in ["dynamic", "cocoapods", "carthage"]) {
        parallelBuilds["Installation - ${platformName} Obj-C ${test}"] = installationTest(platform, test, 'objc')
      }
    }

    parallelBuilds["Installation - iOS Obj-C static"] = installationTest('ios', 'static', 'objc')
    parallelBuilds["Installation - iOS Obj-C CocoaPods dynamic"] = installationTest('ios', 'cocoapods-dynamic', 'objc')
    parallelBuilds["Installation - iOS Swift Package Manager"] = spmInstallationTest('ios')

    parallelBuilds["Installation - Mac Catalyst Obj-C CocoaPods"] = installationTest('catalyst', 'cocoapods', 'objc')
    parallelBuilds["Installation - Mac Catalyst Obj-C CocoaPods dynamic"] = installationTest('catalyst', 'cocoapods-dynamic', 'objc')

    for (def platform in ["osx", "ios", "watchos"]) {
      def platformName = platformNames[platform]
      for (def test in ["dynamic", "cocoapods", "carthage"]) {
        parallelBuilds["Installation - ${platformName} Swift ${test}"] = installationTest(platform, test, 'swift')
      }
    }

    parallelBuilds["Installation - Mac Catalyst Swift CocoaPods"] = installationTest('catalyst', 'cocoapods', 'swift')

    parallel parallelBuilds
  }
}

try {
  doBuild()
} catch (e) {
  // If there was an exception thrown, the build failed
  currentBuild.result = "FAILED"
  throw e
}

